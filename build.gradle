buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath files('libs/liteloader-1.8-SNAPSHOT-mcpnames.jar')
    }
}

//apply plugin: 'liteloader'
apply plugin: 'forge' // No liteloader plugin for 1.8 yet
apply plugin: 'maven-publish'

version = "1.8-" + getVersion()
group = "acs.tabbychat"
archivesBaseName = "TabbyChat"

minecraft {
    version = "1.8-11.14.3.1450"
    runDir = "run"
    mappings = "snapshot_20141130"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "revision", getVersion()
    
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'litemod.json'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'revision':getVersion()
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', 'litemod.json'
    }
}

configurations{
    customCompile
}
dependencies{
    customCompile 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'
    compile configurations.customCompile
}

jar{
    from sourceSets.api.output
    from ( configurations.customCompile.collect { it.isDirectory() ? it : zipTree(it)}) { exclude 'META-INF/**' }
    exclude 'acs/tabbychat/core/LiteModTabbyChat.class', 'litemod.json'
}

def getVersion(project) {
    String version = "$System.env.TRAVIS_BUILD_NUMBER"
    if (version?.trim()) {
    	return version
    }
    return "undefined"
}
